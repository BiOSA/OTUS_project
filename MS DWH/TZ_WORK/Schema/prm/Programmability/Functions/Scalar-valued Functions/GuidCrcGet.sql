-- =============================================
-- Author:		Дзреян К.Е..
-- Create date: 2022-10-10
-- Description:	Проверка УИд
-- в формате файла это Приложение 8.
/*
Формат:
NNNNNNNN-NNNN-1NNN-PNNN-NNNNNNNNNNNN-С, 
где:
N – цифра в шестнадцатеричной системе счисления, в которой используются цифры от 0 до 9 и латинские буквы от 
«a» до «f»;
P – символ, которой может принимать только значения 8, 9, «a» или «b»;
С – контрольный символ.
Контрольный символ рассчитывается следующим образом: 
Шаг 1. Из УИд исключаются все символы дефиса «-», на выходе получается последовательность из 32 чисел.
Шаг 2. В последовательности чисел шестнадцатеричные цифры, обозначаемые буквами латинского алфавита, 
заменяются десятичными числами.
a b c d e f
10 11 12 13 14 15
Шаг 3. В последовательности чисел каждому числу слева направо циклически присваивается порядковый номер от 
1 до 10. 
Шаг 4. Каждое число с последовательности чисел умножается на присвоенный ему порядковый номер.
Шаг 5. Полученные произведения суммируются, затем делятся на 16. Если остаток от деления представляет собой 
двухзначное число, оно преобразовывается в шестнадцатеричную цифру в соответствии с таблицей шага 2. Остаток 
от деления в шестнадцатеричном представлении указывается в качестве контрольного символа УИД.
Примечание. УИд, заполненный значением '00000000-0000-0000-0000-000000000000-0', считается некорректным
*/
-- =============================================
CREATE FUNCTION dbo.GuidCrcGet
(
	@uid UNIQUEIDENTIFIER
)
RETURNS CHAR(1)
AS BEGIN
	DECLARE 
		  @sum    BIGINT      = 0
		, @j      INT         = 1
		, @uids   VARCHAR(50) = REPLACE(CAST(@uid AS VARCHAR(50)), '-', '')
		, @i      INT         = 1;

	WHILE @i<=LEN(@uids) BEGIN
		SELECT @sum += CONVERT(INT, CONVERT(VARBINARY, '0x0'+SUBSTRING(@uids, @i, 1), 1)) * @j;

		SELECT @i+=1, @j = IIF(@j=10, 1, @j+1);
	END

	RETURN FORMAT(@sum%16, 'X');
END